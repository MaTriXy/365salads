apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

static def evalVersionCode() {
    def process = "git rev-list --all --count".execute()
    return process.text.toInteger() + 30
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "com.kan.salads"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode evalVersionCode()
        versionName "1.0.${evalVersionCode()}"
    }

    signingConfigs {
        debug {
            keyAlias "debug-alias"
            keyPassword "password"
            storeFile file("../signing/debug/keystore.debug.jks")
            storePassword "password"
        }
        release {
            keyAlias project.hasProperty("keyAlias") ? project["keyAlias"] : "false"
            keyPassword project.hasProperty("keyPassword") ? project["keyPassword"] : "false"
            storeFile file(project.hasProperty("storeFile") ? project["storeFile"] : "../signing/debug/keystore.debug.jks")
            storePassword project.hasProperty("storePassword") ? project["storePassword"] : "false"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            debuggable = true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        //don't pre dex on the ci build - pointless and takes extra time
        preDexLibraries !project.hasProperty("isCiBuild")
        javaMaxHeapSize "12g"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    implementation 'com.google.firebase:firebase-invites:16.0.0'
    implementation 'com.google.firebase:firebase-auth:16.0.1'
    implementation 'com.google.firebase:firebase-database:16.0.1'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'

    implementation 'com.android.support.constraint:constraint-layout:1.1.1'
    implementation 'com.twitter.sdk.android:twitter:3.0.0'

    implementation "com.jakewharton.timber:timber:4.6.0"
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.3'

}

repositories {
    mavenCentral()
}

play {
    jsonFile = file(project.hasProperty("deployJsonFile") ? project["deployJsonFile"] : "intentionally missing")
    track = 'internal' // Or any of 'rollout', 'beta', 'alpha' or 'internal'
    untrackOld = true
    //userFraction = 0.2 // Only necessary for 'rollout'. The default is 0.1 (10%).
}

apply plugin: 'com.google.gms.google-services'